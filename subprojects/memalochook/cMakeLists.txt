project(memalochook LANGUAGES CXX ASM_NASM)
set(CMAKE_ASM_NASM_OBJECT_FORMAT win32)
# add_compile_definitions(POSITIONINDEPENDENT=0)

set(CMAKE_ASM_NASM_DEBUG_FORMAT "dwarf2" CACHE STRING "Debug format for NASM")

add_subdirectory("${CMAKE_SOURCE_DIR}/include/minhook" "${CMAKE_BINARY_DIR}/minhook")

add_library(memalochook SHARED 
    main.cpp
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/memmove32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/memcpy32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/instrset32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/unalignedisfaster32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/cachesize32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/cputype32.asm"
)

target_include_directories(memalochook PRIVATE
    "${CMAKE_SOURCE_DIR}/include/minhook/include"
    "${CMAKE_SOURCE_DIR}/include/vectorclass"
)

target_link_libraries(memalochook PRIVATE minhook shlwapi)

if(MSVC)
    message(STATUS "Configuring for MSVC compiler")

    set(STATIC_MSVC_RUNTIME "MultiThreaded$<$<CONFIG:Debug>:Debug>")

    set_target_properties(memalochook PROPERTIES
        MSVC_RUNTIME_LIBRARY ${STATIC_MSVC_RUNTIME}
    )
    set_target_properties(minhook PROPERTIES
        MSVC_RUNTIME_LIBRARY ${STATIC_MSVC_RUNTIME}
    )

    target_compile_options(memalochook PRIVATE
        $<$<COMPILE_LANGUAGE:CXX>:/W4>
        $<$<COMPILE_LANGUAGE:CXX>:/EHsc>
        $<$<COMPILE_LANGUAGE:CXX>:/arch:AVX2>
        $<$<CONFIG:Release>:$<$<COMPILE_LANGUAGE:CXX>:/O2 /Ot /fp:fast>>
        $<$<CONFIG:Debug>:$<$<COMPILE_LANGUAGE:CXX>:/Od>>
    )

    target_link_options(memalochook PRIVATE
        /DEBUG
    )

endif()