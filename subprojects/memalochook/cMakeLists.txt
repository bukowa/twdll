cmake_minimum_required(VERSION 3.10)
project(memalochook CXX ASM_NASM)
set(CMAKE_ASM_NASM_OBJECT_FORMAT win32)
add_compile_definitions(POSITIONINDEPENDENT=0)

set(CMAKE_ASM_NASM_DEBUG_FORMAT "dwarf2" CACHE STRING "Debug format for NASM")


add_subdirectory("${CMAKE_SOURCE_DIR}/include/minhook" "${CMAKE_BINARY_DIR}/minhook")

set(MEMALOCHOK_SOURCES
    main.cpp
    asmlib_hooks.cpp
    hook_manager.cpp
    log.cpp
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/memmove32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/memcpy32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/memset32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/memcmp32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/instrset32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/unalignedisfaster32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/cachesize32.asm"
    "${CMAKE_SOURCE_DIR}/include/asmlib/asmlibSrc/cputype32.asm"
)

add_library(memalochook SHARED ${MEMALOCHOK_SOURCES})

target_include_directories(memalochook 
    PRIVATE
    "${CMAKE_SOURCE_DIR}/include/minhook/include"
    PUBLIC
    "${CMAKE_SOURCE_DIR}/include"
    "${CMAKE_SOURCE_DIR}/include/spdlog"
)

target_compile_options(memalochook PRIVATE
    $<$<COMPILE_LANGUAGE:CXX>:$<$<CXX_COMPILER_ID:MSVC>:/utf-8>>
)

target_link_libraries(memalochook PRIVATE minhook shlwapi)

if(MSVC)
    message(STATUS "Configuring for MSVC compiler")

    set(STATIC_MSVC_RUNTIME "MultiThreaded$<$<CONFIG:Debug>:Debug>")

    set_target_properties(memalochook PROPERTIES
        MSVC_RUNTIME_LIBRARY ${STATIC_MSVC_RUNTIME}
    )
    set_target_properties(minhook PROPERTIES
        MSVC_RUNTIME_LIBRARY ${STATIC_MSVC_RUNTIME}
    )

    target_compile_options(memalochook PRIVATE
        $<$<COMPILE_LANGUAGE:CXX>:/W4>
        $<$<COMPILE_LANGUAGE:CXX>:/EHsc>
        $<$<CONFIG:Release>:$<$<COMPILE_LANGUAGE:CXX>:/O2 /Ot /fp:fast>>
        $<$<CONFIG:Debug>:$<$<COMPILE_LANGUAGE:CXX>:/Od>>
    )

    target_link_options(memalochook PRIVATE
        /DEBUG
    )

endif()